######################################################
# O'Doherty & Sabes (2017) 
######################################################

DATASET = "odoherty_sabes_nonhuman_2017"

RAW_DIR = config["RAW_DIR"]
PROCESSED_DIR = config["PROCESSED_DIR"]

BROADBAND_DATASETS = [
    "indy_20160622_01", "indy_20160624_03", "indy_20160627_01", "indy_20160630_01", "indy_20160915_01",
    "indy_20160916_01", "indy_20160921_01", "indy_20160927_04", "indy_20160927_06", "indy_20160930_02",
    "indy_20160930_05", "indy_20161005_06", "indy_20161006_02", "indy_20161007_02", "indy_20161011_03",
    "indy_20161013_03", "indy_20161014_04", "indy_20161017_02", "indy_20161024_03", "indy_20161025_04",
    "indy_20161026_03", "indy_20161027_03", "indy_20161206_02", "indy_20161207_02", "indy_20161212_02",
    "indy_20161220_02", "indy_20170123_02", "indy_20170124_01", "indy_20170127_03", "indy_20170131_02"
]

ZENODO_IDS = [
    1488441, 1486148, 1484825, 1473704, 1467954, 1467051, 1451794, 1433943, 1432819, 1421881, 1421311, 1419774,
    1419173, 1413593, 1412636, 1412095, 1411979, 1411883, 1411475, 1410424, 1321265, 1321257, 1303721, 1302867,
    1302833, 1301046, 1167965, 1163026, 1161225, 1300956
]

rule all:
    input:
        f"{PROCESSED_DIR}/{DATASET}/description.mpk"

rule download_primary_dataset:
    output:
        mat_files = expand(f"{RAW_DIR}/{DATASET}/{{dataset}}.mat", dataset=BROADBAND_DATASETS)
    shell:
        f"""
        mkdir -p {RAW_DIR}/{DATASET}
        zenodo_get 583331 -o {RAW_DIR}/{DATASET}
        """

rule download_broadband_dataset:
    output:
        nwb_file = f"{RAW_DIR}/{DATASET}/broadband/{{dataset_name}}.nwb"
    params:
        zenodo_id = lambda wildcards: ZENODO_IDS[BROADBAND_DATASETS.index(wildcards.dataset_name)],
        parent_dir = f"{RAW_DIR}/{DATASET}/broadband/"
    shell:
        """
        zenodo_get {params.zenodo_id} -o {params.parent_dir}
        """

rule download_all:
    input:
        nwb_files = expand(f"{RAW_DIR}/{DATASET}/broadband/{{dataset}}.nwb", dataset=BROADBAND_DATASETS),
        mat_files = expand(f"{RAW_DIR}/{DATASET}/{{dataset}}.mat", dataset=BROADBAND_DATASETS)

rule prepare_data:
    input:
        py_script = f"data/scripts/{DATASET}/prepare_data.py",
        mat_files = expand(f"{RAW_DIR}/{DATASET}/{{dataset}}.mat", dataset=BROADBAND_DATASETS),
        nwb_files = expand(f"{RAW_DIR}/{DATASET}/broadband/{{dataset}}.nwb", dataset=BROADBAND_DATASETS)
    output:
        description = f"{PROCESSED_DIR}/{DATASET}/description.mpk"
    shell:
        f"""
        mkdir -p {PROCESSED_DIR}/{DATASET}
        python data/scripts/{DATASET}/prepare_data.py --input_dir {RAW_DIR}/{DATASET} --output_dir {PROCESSED_DIR}/{DATASET}
        """

include: "../freeze.smk"